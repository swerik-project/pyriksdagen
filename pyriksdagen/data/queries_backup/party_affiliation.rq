SELECT DISTINCT ?wiki_id ?partyLabel ?start ?end ?startPrecision ?endPrecision
WHERE
{    
    ?wiki_id wdt:P31 wd:Q5;
             p:P102 ?pParty.
    ?pParty ps:P102 ?party.
    OPTIONAL { ?pParty pqv:P580 [wikibase:timePrecision ?startPrecision; wikibase:timeValue ?start ] }
    OPTIONAL { ?pParty pqv:P582 [wikibase:timePrecision ?endPrecision; wikibase:timeValue ?end ] }

    {
    # Members of parliament
    VALUES ?chamber { wd:Q10655178 wd:Q33071890 wd:Q81531912 }
    ?wiki_id p:P39 ?posheld.
    ?posheld ps:P39 ?chamber.
    } UNION {
    # Speakers
    VALUES ?role { wd:Q1850749 wd:Q85679583 wd:Q85679895 wd:Q110785766 wd:Q110785785 wd:Q110811796 wd:Q110811805 wd:Q110812751 wd:Q110812759 }
    ?wiki_id p:P39 ?posheld.
    ?posheld ps:P39 ?role.
    } UNION {
    # Ministers 1
      {
    ?government wdt:P31 wd:Q930729.
    } UNION {
    ?government wdt:P31 wd:Q640506.
    ?government wdt:P17 wd:Q34.
    }
    ?government p:P527 ?pMinister.
    ?pMinister ps:P527 ?wiki_id
    } UNION {
    # Ministers 2
    ?wiki_id wdt:P31 wd:Q5;
             p:P39 ?posheld.
    ?government wdt:P31 wd:Q930729.
    ?posheld pq:P5054 ?government.
    }
  SERVICE wikibase:label { bd:serviceParam wikibase:language "sv". }
}